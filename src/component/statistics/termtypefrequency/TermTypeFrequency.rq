PREFIX a-popis-dat-pojem: <http://onto.fel.cvut.cz/ontologies/slovník/agendový/popis-dat/pojem/>
PREFIX z-sgov-pojem: <https://slovník.gov.cz/základní/pojem/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/terms/>

CONSTRUCT {
    ?pojem  a ?typ;
            a-popis-dat-pojem:je-pojmem-ze-slovníku ?slovnik .
    ?slovnik rdfs:label ?labelSlovniku .

    ?type rdfs:label ?labelTypu .
} WHERE {
	{
	    ?pojem a skos:Concept ;
       	a-popis-dat-pojem:je-pojmem-ze-slovníku ?slovnik .
        OPTIONAL {
   	        ?slovnik dc:title ?labelS .
   	    }
  	    BIND(COALESCE(?labelS,str(?slovnik)) as ?labelSlovniku)

  	    OPTIONAL {
            ?pojem a ?t .
        	FILTER(?t in (z-sgov-pojem:typ-objektu,
                          z-sgov-pojem:typ-vlastnosti,
                          z-sgov-pojem:typ-vztahu,
                          z-sgov-pojem:typ-události ))
        }
        BIND(COALESCE(?t,a-popis-dat-pojem:not-filled) as ?typ)
    } UNION {
        VALUES (?type) { (z-sgov-pojem:typ-objektu)
                         (z-sgov-pojem:typ-vlastnosti)
                         (z-sgov-pojem:typ-vztahu)
                         (z-sgov-pojem:typ-události) }
  	    OPTIONAL {
            ?type rdfs:label ?lT .
        }
    	BIND(COALESCE(?lT,str(?type)) as ?labelTypu)
    }
}
