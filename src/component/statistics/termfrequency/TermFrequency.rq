PREFIX pdp: <http://onto.fel.cvut.cz/ontologies/slovník/agendový/popis-dat/pojem/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX termit: <http://onto.fel.cvut.cz/ontologies/application/termit/pojem/>
PREFIX dc: <http://purl.org/dc/terms/>

CONSTRUCT {
    ?slovnik termit:has-count ?count .
    ?slovnik rdfs:label ?labelSlovniku .
} WHERE {
    SELECT ?slovnik ?labelSlovniku ( COUNT(DISTINCT ?pojem) AS ?count ) {
	    ?pojem a skos:Concept ;
           	pdp:je-pojmem-ze-slovníku ?slovnik .
           	FILTER NOT EXISTS {
                ?slovnik a pdp:verze-slovníku .
            }
        OPTIONAL {
       	    ?slovnik dc:title ?labelS .
       	}
  	    BIND(COALESCE(?labelS,str(?slovnik)) as ?labelSlovniku)
  	} GROUP BY ?slovnik ?labelSlovniku
}
